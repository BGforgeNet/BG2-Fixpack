// fixed stuff at the top of the effects batches
DEFINE_PATCH_MACRO ~add_fx_batch_prep~ BEGIN

  PATCH_IF ("%SOURCE_FILE%" STRING_COMPARE_REGEXP "^.+\.spl" = 0) BEGIN
    READ_LONG   0x64 "abil_off" ELSE 0
    READ_SHORT  0x68 "abil_num" ELSE 0
    READ_LONG   0x6a "fx_off"   ELSE 0
    SET "counter_offset" = 0x70
    SET "abil_length" = 0x28
    SET "global_loop" = 0
    SET "fx_type" = 0
    SET "min_size" = 0x72
    SET "cosmetic" = 1
  END ELSE
  PATCH_IF ("%SOURCE_FILE%" STRING_COMPARE_REGEXP "^.+\.itm" = 0) BEGIN
    READ_LONG   0x64 "abil_off" ELSE 0
    READ_SHORT  0x68 "abil_num" ELSE 0
    READ_LONG   0x6a "fx_off"   ELSE 0
    SET "counter_offset" = 0x70
    SET "abil_length" = 0x38
    SET "global_loop" = 1
    SET "fx_type" = 0
    SET "min_size" = 0x72
    READ_BYTE 0x18 "flags" ELSE 0
    PATCH_IF (("%flags%" BAND "0b00000100") = "0b00000100") BEGIN // if droppable, add portrait icons and whatnot
      SET "cosmetic" = 1
    END ELSE BEGIN
      SET "cosmetic" = 0
    END
  END ELSE
  PATCH_IF ("%SOURCE_FILE%" STRING_COMPARE_REGEXP "^.+\.cre" = 0) BEGIN
    SET "abil_off" = 0
    SET "abil_num" = 0
    READ_LONG  0x2c4 "fx_off" ELSE 0
    SET "counter_offset" = 0x2c8
    SET "abil_length" = 0
    SET "global_loop" = 1
    READ_BYTE 0x33 "fx_type" ELSE 2
    SET "min_size" = 0x2d4
    SET "cosmetic" = 0
  END
  SET "debug" = 0

END

// fixed stuff at the bottom of the effects batches
DEFINE_PATCH_MACRO ~add_fx_batch_final~ BEGIN

  PATCH_IF (("%SOURCE_FILE%" STRING_COMPARE_REGEXP "^.+\.cre" = 0) AND ("%new_fx%" > 0)) BEGIN // fix offsets for cre files if fx inserted
    SET "inserted" = ((0x30 + (0xd8 * "%fx_type%")) * "%new_fx%")
    READ_LONG 0x2a0 "known_off"
    PATCH_IF ("%fx_off%" < "%known_off%") BEGIN
      WRITE_LONG 0x2a0 ("%known_off%" + "%inserted%")
    END
    READ_LONG 0x2a8 "mem_info"
    PATCH_IF ("%fx_off%" < "%mem_info%") BEGIN
      WRITE_LONG 0x2a8 ("%mem_info%" + "%inserted%")
    END
    READ_LONG 0x2b0 "mem_off"
    PATCH_IF ("%fx_off%" < "%mem_off%") BEGIN
      WRITE_LONG 0x2b0 ("%mem_off%" + "%inserted%")
    END
    READ_LONG 0x2b8 "slot_off"
    PATCH_IF ("%fx_off%" < "%slot_off%") BEGIN
      WRITE_LONG 0x2b8 ("%slot_off%" + "%inserted%")
    END
    READ_LONG 0x2bc "itm_off"
    PATCH_IF ("%fx_off%" < "%itm_off%") BEGIN
      WRITE_LONG 0x2bc ("%itm_off%" + "%inserted%")
    END
  END
  PATCH_IF (("%new_fx%" > 0) AND ("%debug%" = 1)) BEGIN
    READ_LONG 0x0c "name"
    PATCH_IF ("%name%" > 0) AND ("%name%" < 80000) BEGIN
      READ_STRREF 0x0c "name"
    END ELSE BEGIN
      READ_STRREF 0x08 "name"
    END
    PATCH_PRINT "              ~%SOURCE_FILE%~   ~override~ // %name%"
  END

END