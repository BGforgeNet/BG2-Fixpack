BEGIN ~new dispel test~ DESIGNATED 3000

// disable broken stuff from key file
ACTION_FOR_EACH file IN iplot01k.itm iplot04g.itm iplot04h.itm iplot04i.itm xr2400.are xr2600.are BEGIN
  DISABLE_FROM_KEY "%file%"
END

INCLUDE ~bg2fixpack/lib/functions.tpa~ // miscellaneous patch functions
INCLUDE ~bg2fixpack/lib/bg2fp_effect_batches2.tpa~ // batch patches, mainly immunity stuff

COPY ~bg2fixpack/temp_copy.tpa~ ~bg2fixpack/temp_copy.tpa~
  REPLACE_TEXTUALLY ~BUT_ONLY~ ~~
  REPLACE_TEXTUALLY "~override~" "~cdhold~"

REINCLUDE ~bg2fixpack/temp_copy.tpa~

OUTER_SET         offset = 0x275
OUTER_SPRINT offset_text ~0x275~
OUTER_SPRINT ids_file gender

<<<<<<<< inlined/temp.ids
ACTION_DEFINE_ASSOCIATIVE_ARRAY temp_staging BEGIN


>>>>>>>>

COPY ~inlined/temp.ids~ ~override/temp.tpa~
COPY ~inlined/temp.ids~ ~override/final.tpa~
  REPLACE_TEXTUALLY ~temp_staging~ ~cd_bulk_cre_changes_%ids_file%~

ACTION_BASH_FOR ~cdhold~ ~^.+\.cre$~ BEGIN
  COPY "%BASH_FOR_FILESPEC%" "%BASH_FOR_FILESPEC%"
    READ_LONG offset race
    INNER_ACTION BEGIN
      APPEND ~temp.tpa~ ~  %SOURCE_RES% => %race%~
    END
    BUT_ONLY
END

APPEND ~temp.tpa~ ~END~

REINCLUDE ~override/temp.tpa~

ACTION_PHP_EACH temp_staging AS cre => n_align BEGIN

  COPY_EXISTING ~%cre%.cre~ ~override~
    READ_LONG offset o_align
    LOOKUP_IDS_SYMBOL_OF_INT orig ~%ids_file%~ o_align
    LOOKUP_IDS_SYMBOL_OF_INT new  ~%ids_file%~ n_align
    READ_STRREF 0x0c name
    PATCH_IF ("%cre%" STRING_COMPARE_REGEXP ~^.$~ = 0) BEGIN SPRINT spc ~       ~ END ELSE
    PATCH_IF ("%cre%" STRING_COMPARE_REGEXP ~^..$~ = 0) BEGIN SPRINT spc ~      ~ END ELSE
    PATCH_IF ("%cre%" STRING_COMPARE_REGEXP ~^...$~ = 0) BEGIN SPRINT spc ~     ~ END ELSE
    PATCH_IF ("%cre%" STRING_COMPARE_REGEXP ~^....$~ = 0) BEGIN SPRINT spc ~    ~ END ELSE
    PATCH_IF ("%cre%" STRING_COMPARE_REGEXP ~^.....$~ = 0) BEGIN SPRINT spc ~   ~ END ELSE
    PATCH_IF ("%cre%" STRING_COMPARE_REGEXP ~^......$~ = 0) BEGIN SPRINT spc ~  ~ END ELSE
    PATCH_IF ("%cre%" STRING_COMPARE_REGEXP ~^.......$~ = 0) BEGIN SPRINT spc ~ ~ END ELSE
                                                            BEGIN SPRINT spc ~~ END
    PATCH_IF n_align <  10 BEGIN SPRINT spc2 ~  ~ END ELSE
    PATCH_IF n_align < 100 BEGIN SPRINT spc2 ~ ~ END ELSE
                           BEGIN SPRINT spc2 ~~ END
    INNER_ACTION BEGIN
      APPEND ~final.tpa~ ~  %cre%%spc% => %spc2%%n_align% // %name%, from %orig% to %new%~
    END
    BUT_ONLY
END

APPEND ~final.tpa~ "
END

ACTION_PHP_EACH cd_bulk_cre_changes_%ids_file% AS cre_file => n_%ids_file% BEGIN

  COPY_EXISTING ~%cre_file%.cre~ ~override~
    WRITE_LONG %offset_text% n_%ids_file%
    BUT_ONLY

END"

MOVE ~override/temp.tpa~ ~bg2fixpack/temp.tpa~
MOVE ~override/final.tpa~ ~bg2fixpack/final.tpa~













BEGIN ~override comp~ DESIGNATED 4000

COPY ~cmp.txt~ ~override/cmp.txt~
COPY ~cmp.txt~ ~override/missing.txt~

ACTION_BASH_FOR ~override~ ~^.+\.spl$~ BEGIN

  ACTION_IF FILE_EXISTS ~..\bg2_tob_v10\override\%BASH_FOR_FILE%~ BEGIN

    COPY ~..\bg2_tob_v10\override\%BASH_FOR_FILE%~ ~..\bg2_tob_v10\override\%BASH_FOR_FILE%~
      READ_ASCII 0x00 v10 (%SOURCE_SIZE%)
      BUT_ONLY

    COPY ~override/%BASH_FOR_FILE%~ ~override/%BASH_FOR_FILE%~
      READ_ASCII 0x00 v11 (%SOURCE_SIZE%)
      PATCH_IF ("%v10%" STRING_COMPARE_CASE "%v11%" = 0) BEGIN
        SET run_comp = 0
      END ELSE BEGIN
        SET run_comp = 1
      END
      BUT_ONLY
      
    ACTION_IF run_comp = 1 THEN BEGIN
    
      PRINT ~comping %BASH_FOR_FILE%~
      AT_NOW ~weidu --cmp-from override\%BASH_FOR_FILE% --cmp-to ..\bg2_tob_v10\override\%BASH_FOR_FILE% >> override\cmp.txt~
    
    END

  END ELSE BEGIN

    APPEND ~missing.txt~ ~%BASH_FOR_FILE% exists for v11 but not v10~

  END

END

ACTION_BASH_FOR ~override_v10~ ~^.+$~ BEGIN

  ACTION_IF NOT FILE_EXISTS ~override/%BASH_FOR_FILE%~ BEGIN

    APPEND ~missing.txt~ ~%BASH_FOR_FILE% exists for v10 but not v11~

  END

END

COPY_EXISTING ~cmp.txt~ ~override~
  REPLACE_TEXTUALLY ~^\[.+$~ ~~
  REPLACE_TEXTUALLY ~^//.+$~ ~~

MOVE ~override/cmp.txt~ ~cmp_final.txt~
MOVE ~override/missing.txt~ ~missing_final.txt~
